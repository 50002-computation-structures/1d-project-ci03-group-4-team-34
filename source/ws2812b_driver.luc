module ws2812b_driver #(
    PIXEL_COUNT = 256 : PIXEL_COUNT > 0    // The number of LEDs in the chain.
)(
    input clk,  // clock
    input rst,  // reset
    input update, // feed '1' to update the display with new 'color', if '0' then it will stay displaying its old value
    input color[24], // BRG, 8 bits each, color to render for all LED
    input clear, // this will clear the LED matrix regardless of update 
    output pixel_address[$clog2(PIXEL_COUNT)],  // Index of the LED, with the color in color[24]
    output data,  //Goes to the first LED in the chain
    output next_pixel, // this turns to 1 for 1 FPGA clock cycle each time we are done with writing a pixel 
    output reset, // this turns to 1 when we are at RESET state
    output done // this turns to 1 once we have refreshed all pixels in the LED
) {
    
    enum States {
        SEND_PIXEL,
        RESET,
        CLEAR
    }
    
    .clk(clk) {
        .rst(rst) {
            dff state[$width(States)](#INIT(States.RESET)) // start at RESET state, don't show anything yet
            dff pixel_address_ctr[$clog2(PIXEL_COUNT)](#INIT(0))
            dff bit_ctr[5]
            dff ctr[7]
            dff rst_ctr [$clog2(5000)] // reset requires 50us of zeroes 
            dff update_request(#INIT(0)) // to store update request so we don't miss it 
            dff clear_request(#INIT(1)) // to store clear request so we don't miss it, initialised to 1 so we always clear LED upon boot
        }
    }
    
    always {
        data = 0
        reset = 0 // not resetting by default
        next_pixel = 0 // not done with any pixel
        
        // dff connections
        rst_ctr.d = rst_ctr.q
        pixel_address_ctr.d = pixel_address_ctr.q
        state.d = state.q
        bit_ctr.d = bit_ctr.q 
        ctr.d = ctr.q 
        update_request.d = update_request.q
        clear_request.d = clear_request.q
        pixel_address = pixel_address_ctr.q
        
        // always store update request if it's not set 
        if (~update_request.q){
            update_request.d = update 
        }
        
        // always store clear request if it's not set
        if (~clear_request.q){
            clear_request.d = clear 
        }
        
        
        
        done = 0
        
        
        case (state.q) {
            States.SEND_PIXEL:
                
                if (color[bit_ctr.q]) // if current bit is HIGH, send 80 1's and 45 0's
                    data = ctr.q < 80
                else // if current bit is LOW, send 40 1's, and 85 0's
                    data = ctr.q < 40
                
                ctr.d = ctr.q + 1
                if (ctr.q == 125){ // check if you can send next bit 
                    ctr.d = 0
                    
                    bit_ctr.d = bit_ctr.q + 1
                    if (bit_ctr.q == 23) { // check if you have sent all 24 bits 
                        bit_ctr.d = 0 // reset bit counter 
                        next_pixel = 1 // safe to change pixel
                        pixel_address_ctr.d = pixel_address_ctr.q + 1 // advance pixel counter
                        if (pixel_address_ctr.q == PIXEL_COUNT-1) { // check if you have sent data to all LEDs
                            done = 1
                            state.d = States.RESET
                        }
                    }
                }
            
            States.RESET:
                
                // this resets the contents of the shift registers after we have latched all the pixel values 
                reset = 1
                // if contents of rst_ctr.q is all 1, we choose what to do: 
                // - remain at RESET and let LEDs display old values, or 
                // - refresh our LEDs with new values
                
                pixel_address_ctr.d = 0 // reset pixel counter
                if (&rst_ctr.q) { 
                    rst_ctr.d = 0
                    if (update_request.q) {
                        update_request.d = 0 // clear the update request
                        state.d = States.SEND_PIXEL // go back to update the entire strip again 
                    }
                    
                    // if both update and clear is 1, then clear will take priority
                    // it will also clear update_request although CLEAR and not SEND_PIXEL is executed
                    if (clear_request.q){
                        clear_request.d = 0 // clear the clear request
                        state.d = States.CLEAR // send all 0s
                    }
                } 
                
                else {
                    // still at reset, continue
                    rst_ctr.d = rst_ctr.q + 1
                }
            
            States.CLEAR:
                
                // send 40 1's, and 85 0's
                data = ctr.q < 40
                
                ctr.d = ctr.q + 1
                if (ctr.q == 125){ // check if you can send next bit 
                    ctr.d = 0
                    
                    bit_ctr.d = bit_ctr.q + 1
                    if (bit_ctr.q == 23) { // check if you have sent all 24 bits 
                        bit_ctr.d = 0 // reset bit counter 
                        pixel_address_ctr.d = pixel_address_ctr.q + 1 // advance pixel counter
                        if (pixel_address_ctr.q == PIXEL_COUNT-1) { // check if you have sent data to all LEDs
                            state.d = States.RESET
                        }
                    }
                }
        }
        
    }
}