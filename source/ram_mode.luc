module ram_mode #(
    COLUMN_DIMENSION = 16d16 : COLUMN_DIMENSION > 0,
    ROW_DIMENSION = 16d16 : ROW_DIMENSION > 1,
    PIXEL_COUNT = 16d256 : PIXEL_COUNT > 0,
    BUFFER_SIZE = 1024 : BUFFER_SIZE > 0 // ensure this value is larger than PIXEL_COUNT * $clog2(COLOR_ENCODING)
) (
    input clk,              // 100MHz clock
    input rst,            // reset button (active low)
    output led[8],          // 8 user controllable LEDs
    output io_led[3][8],    // LEDs on IO Shield
    output io_segment[8],   // 7-segment LEDs on IO Shield
    output io_select[4],    // Digit select on IO Shield
    input io_button[5],     // 5 buttons on IO Shield
    input io_dip[3][8],      // DIP switches on IO Shield
    output data
) {
    
    const CLK_FREQ = $is_sim() ? 1000 : 100000000
    // how many different encodings are used? As example, we use 4 colors, hence ENCODING_AMOUNT is 4
    const ENCODING_AMOUNT = 4 
    
    // OFF WHITE (11), DIM BLUE (10), DIM RED (01), OFF(00)
    const LEDCOLOR = {24h0F0F0F, 24h030000, 24h000300, 24h000000} 
    
    index_reverser index_reverser(#COLUMN_DIMENSION(COLUMN_DIMENSION), #SIZE($clog2(PIXEL_COUNT)))
    
    
    .clk(clk){
        .rst(rst){
            // the data_ram, this is a custom data_ram for demonstration purposes
            // along with custom "player location"
            data_ram ram(#ROW_DIMENSION(ROW_DIMENSION), #ENCODING_AMOUNT(ENCODING_AMOUNT), #COLUMN_DIMENSION(COLUMN_DIMENSION))
            // 0,0 is assumed to be at the top right of LED matrix: Check this with your matrix orientation
            // right ---> x--
            // left ---> x++ 
            // down ---> y++ 
            // up ---> y--
            dff player_x_pos[$clog2(COLUMN_DIMENSION)](#INIT(0))
            dff player_y_pos[$clog2(ROW_DIMENSION)](#INIT(0))
            dff update_ram_flag(#INIT(0))
            ws2812b_driver driver(#PIXEL_COUNT(PIXEL_COUNT)) 
            dff on_off_toggle
        }
    }

    // user inputs 
    button_conditioner movement_button[5](.clk(5x{{clk}}), #CLK_FREQ(5x{{CLK_FREQ}}))
    edge_detector movement_button_edge[5](.clk(5x{{clk}}), #RISE(5x{{1}}), #FALL(5x{{0}}))
    
    
    always {
        led = 8h00            
        io_led = 3x{{8h00}}
        io_segment = 8hff
        io_select = 4hf
        data = 0
        
        player_x_pos.d = player_x_pos.q
        player_y_pos.d = player_y_pos.q
        update_ram_flag.d = update_ram_flag.q
        on_off_toggle.d = on_off_toggle.q
        
        ram.address = 0        // address 0 selected by default
        ram.update = 0         // do not update ram  by default
        movement_button.in = c{io_button[1], io_button[4], io_button[3], io_button[2], io_button[0]} // 0: up, 2: down, 3: left, 4: right
        movement_button_edge.in = movement_button.out
        driver.update = 0 // do not refresh by default
        driver.clear = 0 // do not clear by default
        
        // the sample matrix we use has top RIGHT as 0,0
        // check player movement update 
        // we assume a player is a 2x2 square, the coordinate stored in player_x_pos and player_y_pos indicate top right dot 
        if (movement_button_edge.out[0]){
            // clamp at 0
            if (|player_y_pos.q){
                player_y_pos.d = player_y_pos.q - 1 // up (because up decreases y coordinate in the RAM)
            }
        }
        
        if (movement_button_edge.out[1]){
            // clamp 
            if (player_y_pos.q < COLUMN_DIMENSION-2){
                player_y_pos.d = player_y_pos.q + 1 // down (because down increases y coordinate in the RAM
            }
        }
        
        if (movement_button_edge.out[2]){ // io_button[3]
            // clamp 
            if (player_x_pos.q < COLUMN_DIMENSION-2){
                player_x_pos.d = player_x_pos.q + 1 // left (check the orientation of your own matrix)
            }
        }
        if (movement_button_edge.out[3]){ // io_button[4]
            // clamp at 0 
            if (|player_x_pos.q){
                player_x_pos.d = player_x_pos.q - 1 // right (check the orientation of your own matrix)
            }
        }
         
        if (movement_button_edge.out[4]){
            driver.clear = 1             
            on_off_toggle.d = ~on_off_toggle.q // toggle the on-off to dictate update
        }
        
        // update the ram whenever player moves or io_button[0] is pressed
        if (|movement_button_edge.out[3:0] | (movement_button_edge.out[4] & ~on_off_toggle.q)){
            // update_ram_flag.d = 1
            ram.update = 1
        }
        
        // connect player location to RAM, set update as 0 initially
        ram.player_x_pos = player_x_pos.q 
        ram.player_y_pos = player_y_pos.q

        // connect reverser to led_strip 
        index_reverser.input_address = driver.pixel_address
        
        // always read from ram, pass to driver 
        driver.update = on_off_toggle.q
        // safe to do even though ram read is sequential (+1 clock cycle)
        // because driver always asks for address 0 data by default 
        // and driver will ask for next address only when it is in the last cycle of the current pixel push 
        // it gives time for RAM to load the read data 
        ram.address = index_reverser.output_address 

        
        driver.color = LEDCOLOR[ram.out_encoding] 
        data = driver.data
        
        // debug 
        io_led[0] = player_x_pos.q // player position 
        io_led[1] = player_y_pos.q
        io_led[2] = driver.pixel_address
        
        led[7:5] = ram.debug_data
    }
}